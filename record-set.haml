!!! 5

%link(rel="import" href="../polymer/polymer.html")
%link(rel="import" href="../core-ajax/core-ajax.html")

%polymer-element(name="record-set" attributes="href")
  %template
  :coffeescript
    __data = {}
    __dummy = {data: 0}
    __href = undefined
    __fields = {}
    
    getFields = (record) -> 
      return __fields[record.tagName] if __fields[record.tagName]?

      p = HTMLElement.getPrototypeForTag record.tagName
      loop
        throw "not a record: " + record.tagName unless p.element.extends?
        break if p.element.extends == "record-base"
        p = HTMLElement.getPrototypeForTag p.element.extends
      __fields[record.tagName] = (k for own k,v of p.publish) # rid is excluded
    
    Polymer
      href: undefined
      _data: undefined
      _dummy: undefined
      computed:
        all: '_extractRecords(_data, _dummy.data)' # TODO dummy was added so changes within _data are observed
      created: () ->
        @_data = __data
        @_dummy = __dummy
      ready: () ->
        __href = @href if @href?
        @href = __href if @href != __href
      save: (record, remote) ->
        data = record
        if remote?
          remote.rid = remote.id
          delete remote.id
          data = remote
        @_data[record.type] = { records: {}, next: 1 } unless @_data[record.type]?
        r = @_data[record.type].records[data.rid]
        r = {} unless r?
        unless r.rid?
          r.rid = if remote? then remote.rid else ("rs:" + @_data[record.type].next++)
        @_data[record.type].records[r.rid] = r
        for k in getFields record
          r[k] = data[k]
        @_dummy.data++
      load: (record) ->
        if @_data[record.type]? and @_data[record.type].records[record.rid]?
          for k,v of @_data[record.type].records[record.rid]
            record[k] = v unless k == "rid"
      delete: (record) ->
        if @_data[record.type]? and @_data[record.type].records[record.rid]?
          delete @_data[record.type].records[record.rid]
          @_dummy.data++
      _extractRecords: (data) ->
        new ->
          @[type] = (record.rid for id,record of value.records) for type,value of data
          @

%polymer-element(name="record-base" attributes="rid persist type noautoload")
  %template
    %record-set(id="set")
    %core-ajax(id="ajax" handleAs="json" on-core-response="{{_handleResponse}}")
  :coffeescript
    Polymer
      rid: undefined
      persist: undefined
      type: "record"
      noautoload: undefined
      observe: {
        '$.set.all': '_doAutoLoad' # TODO: overkill ! use fine grained events
      }
      computed:
        #type: '_lc(tagName)' # todo auto detection of type name
        all: '$.set.all[type]'
      ready: () ->
        @save() if @persist?
        @load() if @rid? and not @noautoload?
      save: () ->
        @$.set.save @ 
      load: () ->
        @$.set.load @
      delete: () ->
        @$.set.delete @
      pull: () ->
        x = @$.ajax
        x.url = @$.set.href + "/" + @type
        x.method = "GET" 
        x.body = null
        x.go()
      _handleResponse: (e, d) ->
        if Array.isArray d.response
          for record in d.response
            @$.set.save @, record 
        else
          for k, record of d.response
            record.id = k unless record.id?
            @$.set.save @, record
      _doAutoLoad: () ->
        @load() unless @noautoload?
      _lc: (s) -> s.toLowerCase()
      noautoloadChanged: () -> @_doAutoLoad()

